apply plugin: 'com.android.application'
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
android {
  compileSdkVersion 22
  buildToolsVersion "22.0.0"

  signingConfigs {
    release {
      try {
        keyAlias "weeklyenglish"
        storeFile file("../weeklyenglish.jks")
        storePassword KEYSTORE_PASSWORD
        keyPassword KEY_PASSWORD
      } catch (ignored) {
        throw new InvalidUserDataException(
            "You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
      }
    }
  }
  defaultConfig {
    applicationId "com.poepoemyintswe.weeklyenglish"
    minSdkVersion 16
    targetSdkVersion 22
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
  }
  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }

    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile files('libs/lib.jar')
  compile 'com.android.support:appcompat-v7:22.0.0'
  compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
  compile 'com.jakewharton:butterknife:6.0.0'
  compile 'io.realm:realm-android:0.80.0'
  compile 'com.squareup:otto:1.3.5'
  compile 'com.squareup.retrofit:retrofit:1.7.0'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
  compile 'com.android.support:support-v4:22.0.0'
  compile 'com.github.xiprox.errorview:library:1.0.0'
  compile 'com.android.support:recyclerview-v7:21.0.3'
  compile 'com.github.chenupt.android:dragtoplayout:1.2.1@aar'
  compile 'com.pnikosis:materialish-progress:1.2'
  compile 'com.squareup.dagger:dagger:1.2.1'
  compile 'com.melnykov:floatingactionbutton:1.2.0'
}
